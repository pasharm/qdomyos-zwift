import QtQuick 2.7
import Qt.labs.folderlistmodel 2.15
import QtQuick.Layouts 1.3
import QtQuick.Controls 2.15
import QtQuick.Controls.Material 2.0
import QtQuick.Dialogs 1.0
import Qt.labs.settings 1.0
import Qt.labs.platform 1.1
import QtWebView 1.1

ColumnLayout {
    signal trainprogram_open_clicked(url name)
    signal trainprogram_open_other_folder(url name)
    signal trainprogram_preview(url name)
    signal trainprogram_autostart_requested()

    property url pendingWorkoutUrl: ""
    property url initialWorkoutUrl: ""

    Settings {
        id: settings
        property real ftp: 200.0
    }

    property var selectedFileUrl: ""
    property bool isSearching: false

    // Modern look: a filled search field, the workouts as cards, pill buttons and the
    // preview page in the app theme; the classic look keeps its list and buttons
    readonly property int modernMargin: Math.max(16, window.contentSideMargin)

    Connections {
        target: rootItem
        function onAndroidDocumentPicked(kind, localUrl) {
            if (kind === "training") {
                trainprogram_open_clicked(localUrl)
            }
        }
    }

    // Called by main.qml on the Android back button: leave the workout preview first
    function handleBack() {
        if (stackView.depth > 1) {
            stackView.pop()
            return true
        }
        return false
    }

    function openWorkoutPreview(fileUrl) {
        if (!fileUrl || fileUrl.toString() === "") {
            return
        }
        pendingWorkoutUrl = fileUrl
        trainprogram_preview(fileUrl)
        stackView.push(detailView)
    }

    Component.onCompleted: {
        Qt.callLater(function() {
            openWorkoutPreview(initialWorkoutUrl)
        })
    }

    // Model for search results
    ListModel {
        id: searchResultsModel
    }

    // Function to perform C++-based recursive search
    function searchRecursively(folderUrl, filter) {
        searchResultsModel.clear()

        if (!filter || filter.trim() === "") {
            isSearching = false
            return
        }

        isSearching = true

        // Call C++ FileSearcher for fast recursive search
        var results = fileSearcher.searchRecursively(folderUrl, filter, ["*.xml", "*.zwo"])

        // Populate search results model
        for (var i = 0; i < results.length; i++) {
            searchResultsModel.append(results[i])
        }
    }

    Loader {
        id: fileDialogLoader
        active: false
        sourceComponent: Component {
            FileDialog {
                id: fileDialog
                title: qsTr("Please choose a file")
                folder: shortcuts.home
                nameFilters: [qsTr("Training programs (*.xml *.zwo)"), qsTr("All files (*)")]
                visible: true
                onAccepted: {
                    var chosenFile = fileDialog.fileUrl || fileDialog.file || (fileDialog.fileUrls && fileDialog.fileUrls.length > 0 ? fileDialog.fileUrls[0] : "")
                    console.log("You chose: " + chosenFile)
                    selectedFileUrl = chosenFile
                    if(OS_VERSION === "Android") {
                        trainprogram_open_other_folder(chosenFile)
                    } else {
                        trainprogram_open_clicked(chosenFile)
                    }
                    close()
                    fileDialogLoader.active = false
                }
                onRejected: {
                    console.log("Canceled")
                    close()
                    fileDialogLoader.active = false
                }
            }
        }
    }

    UiMessageDialog {
        id: deleteDialog
        property url fileUrl: ""
        text: qsTr("Delete workout?")
        informativeText: qsTr("This cannot be undone.")
        // modern look: Cancel / Delete in red, as the other questions that cannot be undone;
        // the native dialog of the classic look keeps its Yes / No
        buttons: modern ? (MessageDialog.Yes | MessageDialog.Cancel) : (MessageDialog.Yes | MessageDialog.No)
        yesText: qsTr("Delete")
        destructive: true
        onYesClicked: {
            if (rootItem.deleteTrainingProgramFile(fileUrl)) {
                pendingWorkoutUrl = ""
                isSearching = false
                stackView.clear()
                stackView.push(masterView)
            }
            visible = false
        }
        onNoClicked: visible = false
    }

    StackView {
        id: stackView
        Layout.fillWidth: true
        Layout.fillHeight: true
        initialItem: masterView

        // MASTER VIEW - Lista Workout
        Component {
            id: masterView

            ColumnLayout {
                spacing: window.ui.modern ? 8 : 5

                Row {
                    id: filterRow
                    Layout.fillWidth: true
                    Layout.topMargin: window.ui.modern ? 8 : 0
                    spacing: window.ui.modern ? 8 : 5
                    leftPadding: window.ui.modern ? modernMargin : 0
                    rightPadding: window.ui.modern ? modernMargin : 0

                    Text {
                        visible: !window.ui.modern
                        text: qsTr("Filter")
                        color: window.ui.ink("white")
                        verticalAlignment: Text.AlignVCenter
                    }

                    UiTextField {
                        id: filterField
                        Layout.fillWidth: true
                        // Row has no fill: the modern field takes what the up button leaves
                        width: window.ui.modern ? filterRow.width - filterRow.leftPadding - filterRow.rightPadding
                                         - (upButton.visible ? upButton.width + filterRow.spacing : 0)
                                     : implicitWidth
                        leftPadding: window.ui.modern ? 44 : undefined
                        placeholderText: qsTr("Search (recursive)...")

                        UiIcon {
                            visible: window.ui.modern
                            anchors.left: parent.left
                            anchors.leftMargin: 10
                            anchors.verticalCenter: parent.verticalCenter
                            width: 22
                            height: 22
                            name: "search"
                            color: window.ui.textMuted
                        }

                        function updateFilter() {
                            var text = filterField.text.trim()

                            if (text === "") {
                                // No filter - use normal folder browsing
                                isSearching = false
                            } else {
                                // Trigger recursive C++ search
                                var baseFolder = "file://" + rootItem.getWritableAppDir() + 'training'
                                searchRecursively(baseFolder, text)
                            }
                        }

                        onTextChanged: {
                            searchTimer.restart()
                        }

                        Timer {
                            id: searchTimer
                            interval: 300
                            repeat: false
                            onTriggered: filterField.updateFilter()
                        }
                    }

                    UiButton {
                        id: upButton
                        text: window.ui.modern ? "" : "←"
                        width: window.ui.modern ? 48 : implicitWidth
                        leftPadding: window.ui.modern ? 0 : undefined
                        rightPadding: window.ui.modern ? 0 : undefined
                        visible: !isSearching
                        onClicked: folderModel.folder = folderModel.parentFolder
                        Accessible.name: qsTr("Parent folder")
                        UiIcon {
                            visible: window.ui.modern
                            anchors.centerIn: parent
                            width: 22
                            height: 22
                            name: "arrow_back"
                            color: window.ui.textMain
                        }
                    }
                }

                ListView {
                    Layout.fillWidth: true
                    Layout.fillHeight: true
                    Layout.leftMargin: window.ui.modern ? modernMargin : 0
                    Layout.rightMargin: window.ui.modern ? modernMargin : 0
                    spacing: window.ui.modern ? 8 : 0
                    clip: window.ui.modern
                    ScrollBar.vertical: ScrollBar {}
                    id: list

                    FolderListModel {
                        id: folderModel
                        nameFilters: ["*.xml", "*.zwo"]
                        folder: "file://" + rootItem.getWritableAppDir() + 'training'
                        showDotAndDotDot: false
                        showDirs: true
                        sortField: "Name"
                        showDirsFirst: true
                    }

                    model: isSearching ? searchResultsModel : folderModel

                    delegate: ItemDelegate {
                        id: workoutDelegate
                        width: ListView.view.width
                        height: window.ui.modern ? 60 : 50

                        // Determine item properties based on which model is active
                        property bool isItemFolder: isSearching ? model.isFolder : folderModel.isFolder(index)
                        property string itemFileName: isSearching ? model.fileName : folderModel.get(index, "fileName")
                        property string itemFileUrl: isSearching ? model.filePath : (folderModel.get(index, 'fileUrl') || folderModel.get(index, 'fileURL'))
                        property string itemRelativePath: isSearching ? model.relativePath : ""

                        background: Rectangle {
                            radius: window.ui.modern ? 16 : 0
                            color: window.ui.modern ? (workoutDelegate.pressed ? window.ui.surfaceHigh : window.ui.surface)
                                         : (ListView.isCurrentItem ? Material.color(Material.Green, Material.Shade800) : Material.backgroundColor)
                        }

                        contentItem: RowLayout {
                            spacing: 10

                            Item {
                                width: window.ui.modern ? 0 : 10
                                height: 1
                            }

                            Text {
                                id: fileIcon
                                visible: !window.ui.modern
                                text: isItemFolder ? "📁" : "📄"
                                font.pixelSize: 24
                            }

                            Rectangle {
                                visible: window.ui.modern
                                Layout.preferredWidth: 40
                                Layout.preferredHeight: 40
                                radius: 20
                                color: window.ui.surfaceHighest
                                UiIcon {
                                    anchors.centerIn: parent
                                    width: 22
                                    height: 22
                                    name: isItemFolder ? "folder" : "list_alt"
                                    color: window.ui.textMuted
                                }
                            }

                            ColumnLayout {
                                Layout.fillWidth: true
                                spacing: 2

                                Text {
                                    id: fileName
                                    Layout.fillWidth: true
                                    text: !isItemFolder ?
                                          itemFileName.substring(0, itemFileName.length-4) :
                                          itemFileName
                                    color: window.ui.modern ? window.ui.textMain
                                         : isItemFolder ? Material.color(Material.Orange)
                                         : (workoutDelegate.ListView.isCurrentItem ? "white" : window.ui.ink("white"))
                                    font.pixelSize: 16
                                    font.weight: window.ui.modern ? Font.DemiBold : Font.Normal
                                    elide: Text.ElideRight
                                }

                                Text {
                                    Layout.fillWidth: true
                                    text: itemRelativePath
                                    color: window.ui.modern ? window.ui.textMuted : Material.color(Material.Grey)
                                    font.pixelSize: 12
                                    elide: Text.ElideMiddle
                                    visible: isSearching && itemRelativePath !== ""
                                }
                            }

                            Text {
                                text: "›"
                                font.pixelSize: 24
                                color: Material.color(Material.Grey)
                                visible: !window.ui.modern && !ListView.isCurrentItem
                            }

                            UiIcon {
                                visible: window.ui.modern
                                Layout.preferredWidth: 22
                                Layout.preferredHeight: 22
                                name: "chevron_right"
                                color: window.ui.textMuted
                            }
                        }

                        onClicked: {
                            list.currentIndex = index

                            if (isItemFolder) {
                                // Navigate to folder (only in browse mode)
                                if (!isSearching) {
                                    folderModel.folder = itemFileUrl
                                }
                            } else if (itemFileUrl) {
                                // Load preview and show detail view
                                openWorkoutPreview(itemFileUrl)
                            }
                        }
                    }

                    focus: true

                    Label {
                        parent: list
                        anchors.centerIn: parent
                        // only once the folder is read: the model fills in asynchronously
                        visible: window.ui.modern && list.count === 0
                                 && (isSearching || folderModel.status === FolderListModel.Ready)
                        text: isSearching ? qsTr("No workouts found") : qsTr("No workouts here")
                        color: window.ui.textMuted
                    }
                }

                UiButton {
                    Layout.fillWidth: true
                    Layout.leftMargin: window.ui.modern ? modernMargin : 0
                    Layout.rightMargin: window.ui.modern ? modernMargin : 0
                    Layout.bottomMargin: window.ui.modern ? 8 : 0
                    height: 50
                    text: qsTr("Other folders")
                    onClicked: {
                        if (Qt.platform.os === "android") {
                            rootItem.openAndroidDocumentPicker("training")
                        } else {
                            fileDialogLoader.active = true
                        }
                    }
                }
            }
        }

        // DETAIL VIEW - Anteprima Workout
        Component {
            id: detailView

            ColumnLayout {
                spacing: 10

                // Header con pulsanti
                RowLayout {
                    Layout.fillWidth: true
                    Layout.margins: 5
                    Layout.leftMargin: window.ui.modern ? modernMargin - 8 : 5
                    Layout.rightMargin: window.ui.modern ? modernMargin : 5
                    spacing: window.ui.modern ? 8 : 10

                    UiButton {
                        text: qsTr("← Back")
                        // Modern look: the toolbar arrow goes back to the list (main.qml)
                        visible: !window.ui.modern
                        onClicked: stackView.pop()
                    }

                    Item { Layout.fillWidth: true }

                    UiButton {
                        text: qsTr("Delete")
                        visible: pendingWorkoutUrl.toString() !== ""
                        danger: true
                        Material.background: Material.Red
                        onClicked: {
                            deleteDialog.fileUrl = pendingWorkoutUrl
                            deleteDialog.visible = true
                        }
                    }

                    UiButton {
                        text: qsTr("Start Workout")
                        highlighted: true
                        Material.background: Material.Green
                        onClicked: {
                            trainprogram_open_clicked(pendingWorkoutUrl)
                            trainprogram_autostart_requested()
                            stackView.pop()
                        }
                    }

                }

                // Descrizione workout
                Text {
                    Layout.fillWidth: true
                    Layout.margins: 10
                    text: rootItem.previewWorkoutDescription
                    font.pixelSize: window.ui.modern ? 16 : 14
                    font.weight: window.ui.modern ? Font.DemiBold : Font.Bold
                    color: window.ui.ink("white")
                    wrapMode: Text.WordWrap
                    horizontalAlignment: Text.AlignHCenter
                }

                Text {
                    Layout.fillWidth: true
                    Layout.leftMargin: 10
                    Layout.rightMargin: 10
                    text: rootItem.previewWorkoutTags
                    font.pixelSize: 12
                    wrapMode: Text.WordWrap
                    color: window.ui.modern ? window.ui.textMuted : Material.color(Material.Grey, Material.Shade400)
                    horizontalAlignment: Text.AlignHCenter
                }

                // WebView con grafico
                // Preview data is now loaded via WebSocket, no runJavaScript needed
                Item {
                    Layout.fillWidth: true
                    Layout.fillHeight: true

                    // The native view is white until the page is loaded: kept hidden till then,
                    // as in WorkoutEditor.qml. It is also drawn above every QML item, so it
                    // hides while the delete question is open, or the question stays under it
                    BusyIndicator {
                        anchors.centerIn: parent
                        visible: !previewWebView.pageLoaded
                        running: visible
                    }

                    WebView {
                        id: previewWebView
                        anchors.fill: parent
                        visible: pageLoaded && !deleteDialog.visible
                        url: "http://localhost:" + settings.value("template_inner_QZWS_port") + "/workoutpreview/preview.html" + window.ui.webThemeFragment()
                        // Modern look: the theme reaches the page in the URL fragment; a later
                        // change only moves the fragment (no reload), so it also goes through
                        // runJavaScript
                        property bool pageLoaded: false
                        readonly property var pageTheme: window.ui.webTheme
                        onPageThemeChanged: if (pageLoaded && pageTheme) runJavaScript(window.ui.webThemeScript())
                        onLoadingChanged: {
                            if (loadRequest.status === WebView.LoadSucceededStatus) {
                                pageLoaded = true
                                if (pageTheme)
                                    runJavaScript(window.ui.webThemeScript())
                            }
                        }
                    }
                }
            }
        }
    }
}
